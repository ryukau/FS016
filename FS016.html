<!DOCTYPE html>
<html>
	<head>
		<meta charset=utf-8>
		<title>FS 016</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
        
        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            
			uniform float time;
			uniform vec2 resolution;
			uniform sampler2D tPrev;

			varying vec2 vUv;
            
            //
            // 便利関数
            //
            vec2 normalizedPosition()
            {
                return -1.0 + 2.0 * vUv;
            }
            
            float rand(vec2 co){
                return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
            }
            
            //
            // scene
            //
            
            const int R = 7; // Range 近傍の範囲
            const int T = 38; // Threshold 状態遷移に必要な状態を持つ近傍の数
            const int C = 4; // Colors 状態の数 (0..C-1)
            const bool N = false; // Neighborhood 近傍の種類。falseならMoore(NM), trueならVon Neumann(NN)
            
            float initialize(vec2 p)
            {
                //// 中央に一つ点を置く
                //vec2 d = (1.0) / resolution;
                //
                //if (0.0 <= p.x && p.x <= d.x && 0.0 <= p.y && p.y <= d.y)
                //    return 1.0 / float(C);
                //    
                //return 0.0;
                
                // ランダムスタート
                float r = rand(gl_FragCoord.xy + vec2(40.0, 2000.0));
                return floor(r * float(C) + 1e-10) / float(C);
            }
            
            int cellState(vec2 delta)
            {
                // C = 6 のとき3状態にしかならない
                return int((float(C) * texture2D(tPrev, vUv + delta).r) + 0.01);
            }
            
            bool match(int mid, vec2 delta)
            {
                if (mid == cellState(delta))
                    return true;
                
                return false;
            }
            
            int sumNM(int mid, vec2 d)
            {
                // Moore neighborhood (8近傍)
                
                int sum = 0;
                for (int x = -R; x <= R; ++x)
                {
                    for (int y = -R; y <= R; ++y)
                    {
                        if (x == 0 && y == 0)
                            continue;
                            
                        if (match(mid, vec2((float(x)) * d.x, (float(y)) * d.y)))
                            sum += 1;
                    }
                }
                return sum;
            }
            
            int sumNN(int mid, vec2 d)
            {
                // Von Neumann neighborhood (4近傍)
                
                int sum = 0;
                
                //
                // 没コード。正しく動くと思われるが、以下の制約から利用できない。
                // 'i' : Loop index cannot be compared with non-constant expression
                //
                //for (int tt = 1; tt <= R; ++tt) // 中央ピクセルからの距離
                //{
                //    for (int i = 0; i < tt; ++i)
                //    {
                //        // 第一象限の座標だけ計算、残りは符号の付け替えによる回転で求める
                //        vec2 p = vec2(float(R - i) * d.x, float(i) * d.y);
                //    
                //        if (match(mid, p)) sum += 1;
                //        if (match(mid, vec2( p.x, -p.y))) sum += 1;
                //        if (match(mid, vec2(-p.x,  p.y))) sum += 1;
                //        if (match(mid, vec2(-p.x, -p.y))) sum += 1;
                //    }
                //}
                
                for (int x = -R; x <= R; ++x)
                {
                    for (int y = -R; y <= R; ++y)
                    {
                        if (x == 0 && y == 0)
                            continue;
                            
                        if (float(R) < (abs(float(x)) + abs(float(y))))
                            continue;
                            
                        if (match(mid, vec2(float(x) * d.x, float(y) * d.y)))
                            sum += 1;
                    }
                }
                return sum;
            }
            
            float calc()
            {
                // 1ピクセルの距離
                vec2 d = (1.0) / resolution;
                
                int cell = cellState(vec2(0.0, 0.0));
                
                // セルの次の状態を求める
                int mid = cell - 1; 
                if (mid < 0) mid = C - 1;
                
                // 次の状態を持つ近傍の数を数える
                int sum = 0;
                if (N)
                    sum = sumNN(mid, d);
                else
                    sum = sumNM(mid, d);
                
                // セルの次の状態を決定する
                if (T <= sum)
                    cell = mid;
                
                return float(cell) / float(C);
            }
            
            //
            // main
            //
			void main()
            {
                float r = 0.0;
                if (time < 1.0)
                {
                    vec2 p = normalizedPosition();
                    r = initialize(p);
                }
                else
                {
                    r = calc();
                }
                
				gl_FragColor = vec4( vec3(r), 1.0 );
			}
        </script>

        <script id="vertexShader" type="x-shader/x-fragment">
            varying vec2 vUv;
            
            void main()
            {
                vUv = uv;
                vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
                gl_Position = projectionMatrix * mvPosition;
            }
        </script>
	</head>
	<body style="overflow:hidden">
        <script src="js/three.min.js"></script>
        <script src="js/scene.js"></script>
	</body>
</html>